grammar dk.sdu.mmmi.mdsd.iot_dsl.IoTDSL with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate ioTDSL "http://www.sdu.dk/mmmi/mdsd/iot_dsl/IoTDSL"

System:
	mqtt=Mqtt externals+=External* statevariables+=StateDeclaration* componenttypes+=ComponentType* server=Server boards+=Board+ logic+=Logic+ expose+=Expose*
;

StateDeclaration:
	'var' type=Type name=ID
;

Mqtt:
	'mqtt' '(' 'host' '=' host=(STRING | IP) ',' 'user' '=' user=STRING ',' 'pass' '=' pass=STRING ',' 'port' '=' port=INT ')'
;

ComponentType:
	({SensorType} 'sensortype' | {ActuatorType} 'actuatortype') name=ID ":" BEGIN initializer=Initializer? properties+=Property* END
;

Property:
	"property" type=Type name=ID
;

Type:
	"string" | "integer" | "float" | "boolean"
;

External:
	"external" type=Type name=ID '(' parameters+=Parameter? (',' parameters+=Parameter)* ')'
;

Initializer:
	"initializer" '(' parameters+=Parameter? (',' parameters+=Parameter)* ')'
;

Parameter:
	type=Type list?=List? name=ID
;

List:
	'[' ']'
;

Server:
	'server' ':' BEGIN 'port' '=' port=INT END
;

IP:
	INT '.' INT '.' INT '.' INT
;

Board:
	'board' name=ID ':'
	BEGIN
		elements+=Element+
	END
;

Element:
	WiFi | Component
;

WiFi:
	'wifi' '(' 'ssid' '=' ssid=STRING ',' 'pass' '=' pass=STRING ')'
;

Component:
	name=ID type=[ComponentType] '(' args+=Argument? (',' args+=Argument)* ')' rate=Rate?
;

Rate:
	'every' time=INT timeUnit=TimeUnit
;

Argument:
	STRING | INT
;

Logic:
	Loop | Statement
;

Loop:
	'every' time=INT timeunit=TimeUnit 'do' ':' BEGIN statements+=Statement+ END
;

TimeUnit:
	'hours' | 'minutes' | 'seconds'
;

Statement:
	((Variable | Assignment) ";"?) | If
;

Variable:
	'var' name=ID '=' exp=Expression
;

Expression:
	Or
;

Or returns Expression:
	And ({Or.left=current} '||' right=And)*
;

And returns Expression:
	Equality ({And.left=current} "&&" right=Equality)*
;

Equality returns Expression:
	Comparison ({Equality.left=current} op=("=="|"!=") right=Comparison)*
;
Comparison returns Expression:
	PlusOrMinus ({Comparison.left=current} op=(">="|"<="|">"|"<") right=PlusOrMinus)*
;
PlusOrMinus returns Expression:
	Factor (({Plus.left=current} '+' | {Minus.left=current} '-') right=Factor)*
;

Factor returns Expression:
	Primary (({Mult.left=current} '*' | {Div.left=current} '/') right=Primary)*
;

Primary returns Expression:
	Reference | Number | Text | ExternalUse | Percentage | FloatNumber | Boolean
;

Boolean:
	value=('true' | 'false')
;

FloatNumber:
	value=FLOAT
;

FLOAT returns ecore::EFloat:
	INT '.' INT
;

Percentage:
	value=INT '%'
;

Reference:
	ref=[Variable] | PropertyUse
;

PropertyUse:
	((board=[Board] '.' component=[Component])| ('*' '.' componenttype=[ComponentType])) '.' property=[Property]
;

Number:
	value=INT
;

Text:
	value=STRING
;

ExternalUse:
	ref=[External] '(' args+=Expression? (',' args+=Expression)* ')'
;

If:
	'if' condition=Expression 'then' BEGIN statements+=Statement+  END elseifs+=ElseIf* else=Else?
;

ElseIf:
	'elseif' condition=Expression 'then' BEGIN statements+=Statement+ END
;

Else:
	'else' BEGIN statements+=Statement+ END
;

Assignment:
	ref=Reference '=' exp=Expression
;


Expose:
	'expose' name=ID ':' BEGIN statements+=Statement+ END
;

terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';